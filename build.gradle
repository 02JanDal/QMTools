if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.multimc.qmtools.qmtools.QMTools'
}

apply plugin: 'application'
apply plugin: 'java'

repositories {
    mavenCentral();
}

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = mainClass;
applicationName = 'QMTools';
group = 'org.multimc'
version = '0.1-SNAPSHOT'

sourceSets {
    main {
        java {
            exclude 'org/multimc/qmlib/**'
        }
    }
    qmlib {
        java {
            exclude 'org/multimc/qmtools/**'
        }
    }
}

task qmlibJar(type: Jar) {
    from sourceSets.qmlib.output
    version = project.version
    baseName = 'QMLib'
}

artifacts {
    archives jar
    archives qmlibJar
}

dependencies {
    qmlibCompile group: 'com.google.code.gson', name: 'gson', version: '2.3'
    compile files("${libsDir}/QMLib-${version}.jar") {
        builtBy 'qmlibJar'
    }
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.7'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
    compile group: 'org.graphstream', name: 'gs-core', version: '1.2'
    compile group: 'org.graphstream', name: 'gs-ui', version: '1.2'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '2.0' //Always try to use latest
}
